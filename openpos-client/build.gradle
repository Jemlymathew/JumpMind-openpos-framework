plugins {
	id "com.moowork.node" version "1.2.0"
}

node { 
  version = '9.3.0' 
  npmVersion = '5.6.0' 
  download = true 
  workDir = file("${openposDir}/node")
  nodeModulesDir = file("./")  
} 
 
task clean {} 
 
task nodeClean(type: Delete) { 
  delete "${openposDir}/node" 
  delete "${openposDir}/node_modules" 
} 
clean.dependsOn nodeClean

task npmClean(type: Delete) {
	delete "./node_modules"
	delete "./dist"
	delete "./coverage"
}
clean.dependsOn(npmClean)

npmSetup.doFirst {
  if (gradle.startParameter.taskNames.contains('develop')) {
	// Use shared copy of node when setting up dev environment.
	// This allows check_core_lib_installed to find openpos-client-core-lib
	// in the developers shared node modules.
    node.download = false; 
  }
}

task check_core_lib_installed(type: NpmTask) {  
  ignoreExitValue true  
  args = ['ls', '--global', 'true', '@jumpmind/openpos-client-core-lib']
}

task npm_link_manual(type: NpmTask) {  
  workingDir = file("${project.projectDir}")
  args = ['link', '@jumpmind/openpos-client-core-lib' ]
}

npm_link_manual.doFirst {
	// Don't try to link if we can't locate the client lib.
    if (check_core_lib_installed.result == null || check_core_lib_installed.result.exitValue > 0) { 
		println '@jumpmind/openpos-client-core-lib symlink not found, will not attempt to link projects.\n' +
		'If you want to link in the @jumpmind/openpos-client-core-lib, you will need to have pulled the openpos-client-core-lib\n' +
		'project, and run \'npm link\' from the home directory of that project.'
		throw new StopExecutionException() 
	} else {
		println 'Linking @jumpmind/openpos-client-core-lib...'
	}
}

npm_link_manual.dependsOn (check_core_lib_installed)

task develop { dependsOn npm_install, npm_link_manual }

task build {}
build.dependsOn(npm_run_build)

task pack {}
npm_pack.dependsOn(build)
pack.dependsOn(npm_pack)

task assemble {}
assemble.dependsOn(build)
